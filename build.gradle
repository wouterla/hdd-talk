buildscript {
    ext {
        kotlin_version = '1.2.41'
        vertx_version = '3.4.2'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow" version "2.0.4"

    id 'org.ysb33r.nodejs.base'  version '0.1'
    id 'org.ysb33r.nodejs.npm'   version '0.1'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

npm {
    executable searchPath()
}

task npmInit( type : org.ysb33r.gradle.nodejs.tasks.NpmTask ) {
    command 'init'
    cmdArgs '-f', '-y'
}

task npmInstall( type : org.ysb33r.gradle.nodejs.tasks.NpmTask ) {
    command 'install'
    cmdArgs '-f', '-y'
}

apply plugin: 'kotlin'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'com.lagerweij.hdd.webshop.MainVerticle'

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/iovertx-3684/"
        url "https://plugins.gradle.org/m2/"
    }
}
dependencies {

    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-web:$vertx_version"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile "biz.paluch.logging:logstash-gelf:1.11.1"
    compile "com.google.code.gson:gson:2.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.9.5"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.5"

    testCompile "io.vertx:vertx-unit:$vertx_version"
    testCompile "junit:junit:4.12"

//    npm npmPackage(name : 'vuejs', tag : '1.0.29')
//    npm npmPackage(name : 'axios', tag : '0.16.2')
}

// Redeploy watcher.
run {
    jvmArgs = [ "-Dtestprop=testval3" ]
    systemProperty("testprop", "testval2")
    args = [ 'run', mainVerticleName,
            "-Djava.util.logging.config.file=" + rootProject.projectDir + "/logging.properties",
            "--launcher-class=$mainClassName",
            "--redeploy=src/**/*.*",
            "--on-redeploy=./gradlew classes"
    ]
}

test {
    testLogging.showStandardStreams = true

    systemProperty 'java.util.logging.config.file', rootProject.projectDir.toString() + "/logging.properties"
}

// Naming and packaging settings for the "shadow jar".
shadowJar {
    baseName = 'webshop'
    classifier = 'shadow'

    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
    into 'webroot', {
		  from 'webroot'
	  }
    into 'node_modules', {
      from 'node_modules'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}

// Heroku relies on the 'stage' task to deploy.
task stage {
    dependsOn shadowJar
}

task buildImage(type: Exec) {
    dependsOn shadowJar

    workingDir '.'
    commandLine 'docker', 'build', '-t', 'wouterla/hdd-talk', '.'
}

task stopContainer(type: Exec) {
    ignoreExitValue = true

    workingDir '.'
    commandLine 'docker', 'rm', '-f', 'hdd-talk'
}

task startContainer(type: Exec) {
    dependsOn buildImage
    dependsOn stopContainer

    workingDir '.'
    commandLine 'docker', 'run', '-d', '-p', '8080:8080', '--name', 'hdd-talk', 'wouterla/hdd-talk'
}

task publishImage(type: Exec) {
    dependsOn buildImage

    workingDir '.'
    commandLine 'docker', 'push', 'wouterla/hdd-talk'
}

task startMinikube(type: Exec) {
    workingDir '.'
    commandLine 'minikube', 'start', '--memory', '4096'
}

task initHelm(type: Exec) {
  mustRunAfter startMinikube
  commandLine 'helm', 'init'
}

task fixHelmConfig(type: Exec) {
  mustRunAfter initHelm
  commandLine 'kubectl', '-n', 'kube-system', 'patch', 'deployment', 'tiller-deploy', '-p', '{"spec": {"template": {"spec": {"automountServiceAccountToken": true}}}}'
}

task startJenkins(type: Exec) {
  mustRunAfter fixHelmConfig
  mustRunAfter initHelm
  commandLine 'helm', 'install', '--name', 'hdd-jenkins', 'stable/jenkins'
}

task showJenkinsPassword(type: Exec) {
  mustRunAfter startJenkins

  commandLine 'bash', '-c', 'printf $(kubectl get secret --namespace default hdd-jenkins -o jsonpath="{.data.jenkins-admin-password}" | base64 --decode);echo'
}

task openJenkins(type: Exec) {
  mustRunAfter startJenkins

  commandLine 'minikube', 'service', 'hdd-jenkins'
}

task startELK(type: Exec) {
  mustRunAfter fixHelmConfig
  mustRunAfter initHelm

  workingDir './ops'
  commandLine 'helm', 'install', '--name', 'elk', './elk-demo-helm'
}

task openKibana(type: Exec) {
  mustRunAfter startELK

  commandLine 'minikube', 'service', 'kibana'
}

task startCluster() {
  dependsOn startMinikube
  dependsOn initHelm
  dependsOn fixHelmConfig
}

task startBaseComponents() {
  dependsOn startELK
  dependsOn startJenkins
  dependsOn showJenkinsPassword
}

task undeploy(type: Exec) {
  ignoreExitValue = true

  commandLine 'helm', 'delete', '--purge', 'hdd-talk'
}

task deploy(type: Exec) {
  dependsOn publishImage
  dependsOn undeploy

  workingDir './ops'
  commandLine 'helm', 'install', '--name', 'hdd-talk', './hdd-talk'
}

task openBookshop(type: Exec) {
  mustRunAfter deploy

  commandLine 'minikube', 'service', 'hdd-talk'
}
